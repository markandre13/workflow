/*
 *  workflow - A collaborative real-time white- and kanban board
 *  Copyright (C) 2018 Mark-André Hopf <mhopf@mark13.org>
 *
 *  This program is free software: you can redistribute it and/or modify
 *  it under the terms of the GNU General Public License as published by
 *  the Free Software Foundation, either version 3 of the License, or
 *  (at your option) any later version.
 *
 *  This program is distributed in the hope that it will be useful,
 *  but WITHOUT ANY WARRANTY; without even the implied warranty of
 *  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *  GNU General Public License for more details.
 *
 *  You should have received a copy of the GNU General Public License
 *  along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

// corba.js IDL hack: native types suffixed with _ptr can be 'undefined'
native AbstractPath;
native Path;
native Point_ptr;
native Matrix_ptr;
native Figure_ptr;
native FigureEditor;
native SVGElement;
native SVGElement_ptr;

valuetype Point {
    public double x, y;
};

valuetype Size {
    public double width, height;
};

// This class is equivalent to the SVGMatrix/DOMMatrix
// ⎡a c e⎤   ⎡x⎤   ⎡x'⎤
// ⎢b d f⎥ ∙ ⎢y⎥ = ⎢y'⎥
// ⎣0 0 1⎦   ⎣1⎦   ⎣1 ⎦
valuetype Matrix {
    public double a, b, c, d, e, f;

    // FIXME: declaring operations will make the resulting class 'abstract',
    // previnting us to use it as an attribute because it can not be
    // instantiated. this is were CORBA's factory would be of help, because
    // it could the used to instantiate the real implementation.
    // or we could use the class registered in the ORB, which is similar to
    // the factory.
    // it might also be a good idea to create the valuetypes as interfaces
    // and to provide a utility function for the code we are currently
    // placing within the constructor. this way we could use inheritance
    // without any prototype hacks when implementing the valuetypes.
    // (can interfaces have static functions? also: factory?)

    Matrix identity();
    /*
    Matrix append(in Matrix matrix);
    Matrix prepend(in Matrix matrix);
    Matrix invert();
    Matrix translate(in Point point);
    Matrix rotate(in double radiant);
    Matrix scale(in double x, in double y);
    Point transformPoint(in Point point);
    // 2 is maximum size, we would like exact size
    // sequence<double, 2> transformArrayPoint(in sequence<double, 2> point);
    sequence<double> transformArrayPoint(in sequence<double> point);

    double getRotation();
    */
};

valuetype Rectangle {
    public Point origin;
    public Size size;

    Rectangle set(in double x, in double y, in double width, in double height);
    boolean contains(in Point point);
    boolean containsRectangle(in Rectangle rectangle);
    boolean inside(in Point point);
    boolean intersects(in Rectangle rectangle);
    Rectangle expandByPoint(in Point point);
    Point center();
    Rectangle expandByRectangle(in Rectangle rectangle);
    Rectangle inflate(in double expansion);

};

valuetype Figure {
    public unsigned long id;
    public Matrix_ptr matrix;

    Path getPath();
    SVGElement updateSVG(in AbstractPath path, in SVGElement parentSVG, in SVGElement_ptr svg); 
    
    boolean transform(in Matrix matrix);
    Rectangle bounds();
    double distance(in Point point);
    Point_ptr getHandlePosition(in unsigned short handleId);
    void setHandlePosition(in unsigned short handleId, in Point position);
};

module figure {
    valuetype AttributedFigure: Figure {
    	public string stroke;
    	public double strokeWidth;
    	public string fill;
    };

    valuetype Shape: AttributedFigure {
        public Point origin;
        public Size size;
    };

    valuetype Rectangle: Shape {
    };

    valuetype Circle: Shape {
    };

    valuetype Text: Shape {
        public string text;
    };

    valuetype PathSegment {
        public char type;
        public sequence<double> data;
    };

    valuetype Path: AttributedFigure {
        public sequence<PathSegment> segments;
    };

    valuetype Group: Figure {
	    public sequence<Figure> childFigures;
    };

    valuetype Transform: Group {
	    // public Matrix matrix; hard to find: this should yield an IDL compiler error
    };
};

valuetype FigureModel {
    public sequence<Figure> data; // FIXME: 'data' is a bad name, how about 'figures'?
};

valuetype Layer: FigureModel {
    public unsigned long id;
    public string name;
    Figure_ptr findFigureAt(in Point point);
};

valuetype User {
    public unsigned long uid;
    public string name;
    public string password;
    public string email;
    public string fullname;
    public string avatar;
};

valuetype Card {
    public unsigned long id;
    public string name;
    public string description;
};

valuetype BoardModel {
    public unsigned long bid;
    public string name;
    public string description;
    public sequence<Layer> layers;
};

/////////////////////////////////////////////////////////////////////////////
//
// Interfaces
//
/////////////////////////////////////////////////////////////////////////////

interface BoardListener {
    oneway void add(in unsigned long layerID, in Figure figure);
    oneway void transform(in unsigned long layerID,
                          in sequence<unsigned long> figureIDs,
                          in Matrix matrix,
                          in sequence<unsigned long> newIDs);
};

interface Board {
    BoardModel getModel();

    oneway void addListener(in BoardListener listener);
    oneway void removeListener(in BoardListener listener);

    oneway void add(in unsigned long layerID, in Figure figure);
    // FIXME? how about transforming figures on different layers at the same time?
    oneway void transform(in unsigned long layerID, in sequence<unsigned long> figureIDs, in Matrix matrix);
};

interface Project {
    Board getBoard(in unsigned long boardID);
};

interface Client {
    // logon
    oneway void logonScreen(in unsigned long lifetime, in string disclaimer, in boolean remember, in string errorMessage);
    oneway void homeScreen(
    	in string cookie,
	in string avatar,
	in string email,
	in string fullname);
};

interface Server {
    // initial contact
    oneway void setClient(in Client client);

    // logon
    oneway void initializeWebSession(in string session);
    oneway void logon(in string user, in string password, in boolean remember);

    // ...
    Project getProject(in unsigned long projectID);
};

interface WorkflowServer {
    Server getServer();
};
